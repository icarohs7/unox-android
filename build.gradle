/*
 * MIT License
 *
 * Copyright (c) 2018 Icaro R D Temponi
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}")
        classpath('com.android.tools.build:gradle:3.3.0')
        classpath("org.jacoco:org.jacoco.core:0.8.2")
    }
}

plugins {
    id("com.github.ben-manes.versions") version "0.20.0"
    id("com.palantir.git-version") version "0.11.0"
}

project.with {
    group = "com.github.icarohs7"
    version = gitVersion().toString().find("(\\d\\.\\d\\.\\d)")
}

subprojects { proj ->
    afterEvaluate {
        android {
            compileSdkVersion(28)

            defaultConfig {
                minSdkVersion(21)
                targetSdkVersion(28)
                testInstrumentationRunner("androidx.test.runner.AndroidJUnitRunner")
                testInstrumentationRunnerArguments(clearPackageData: "true")
            }

            buildTypes {
                debug {
                    testCoverageEnabled(true)
                }
                release {
                    minifyEnabled(false)
                    proguardFiles(getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro")
                }
            }

            dependencies {
                testImplementation(TestDeps.core)
                androidTestImplementation(AndroidTestDeps.core)
                androidTestImplementation("com.schibsted.spain:barista:${Versions.barista}") {
                    exclude(group: "com.android.support")
                    exclude(group: "org.jetbrains.kotlin")
                }
            }

            dataBinding {
                enabled(true)
            }

            compileOptions {
                sourceCompatibility(1.8)
                targetCompatibility(1.8)
            }

            testOptions {
                execution("ANDROIDX_TEST_ORCHESTRATOR")
                unitTests {
                    includeAndroidResources(true)
                    returnDefaultValues(true)
                }
            }

            packagingOptions {
                pickFirst("META-INF/library_release.kotlin_module")
            }
        }
    }
}

allprojects {
    repositories {
        google()
        maven { url "https://jitpack.io" }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}