plugins {
    id("com.github.dcendents.android-maven") version "2.1"
    id("com.android.library")
    id("kotlin-android")
    id("kotlin-android-extensions")
    id("kotlin-kapt")
    id("jacoco")
}

android {
    defaultConfig {
        compileSdkVersion(28)
        versionCode(1)
        versionName(rootProject.version)
    }
}

dependencies {
    implementation(fileTree(dir: "libs", include: ["*.jar"]))
    api(Deps.androidxcore)
    api(Deps.ankoCommons)
    api(Deps.ankoSdk25)
    api(Deps.appCompat)
    api(Deps.arrowCore)
    api(Deps.bungee)
    api(Deps.coroutinesAndroid)
    api(Deps.fragmentKtx)
    api(Deps.glide)
    api(Deps.jsonIter)
    api(Deps.kotlinStdLib)
    api(Deps.lifecycleExtensions)
    api(Deps.maskedEditText)
    api(Deps.materialDesign)
    api(Deps.materialDialogs)
    api(Deps.recyclerView)
    api(Deps.rxJava2)
    kapt(KaptDeps.core)
}

jacoco {
    toolVersion = "0.8.2"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/*$[0-9].*'
    ]
    def debugTree = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/kotlin"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = "sources"
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}